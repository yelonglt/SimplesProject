apply plugin: 'com.android.application'

static def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion compileSdkVersionValue

    defaultConfig {
        applicationId "com.zhongsou.fresco"
        minSdkVersion minSdkVersionValue
        targetSdkVersion targetSdkVersionValue
        versionCode 1
        versionName "1.0"

        // 默认是umeng的渠道
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "umeng"]
    }

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        release {
            storeFile file("souyue-release.keystore")
            storePassword "therycom"
            keyAlias "com.zhongsou.souyue"
            keyPassword "therycom"
        }
    }

    buildTypes {
        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"

            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            //shrinkResources false
            //signingConfig signingConfigs.debug
        }

        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"

            //是否进行代码混淆
            minifyEnabled false
            zipAlignEnabled true
            // 移除无用的resource文件
            //shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "UMENG_CHANNEL_VALUE"
    // 友盟多渠道打包,打包命令，先执行gradle clean后执行gradle assembleRelease
    productFlavors {
        wandoujia {}
        _360 {}
        baidu {}
        xiaomi {}
        tencent {}
        taobao {}
        anzhi {}
    }

    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            // 输出apk名称为boohee_v1.0_2015-01-15_wandoujia.apk
            if (buildType.name.equalsIgnoreCase("release")) {
                def fileName = "girls_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                outputFileName = new File("./", fileName)
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation "junit:junit:$rootProject.ext.junitVersion"
    implementation "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:design:$rootProject.supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"
    implementation "com.facebook.fresco:fresco:$rootProject.fresco"
    implementation "com.google.code.gson:gson:$rootProject.gson"
    implementation "de.greenrobot:eventbus:$rootProject.eventbus"
    implementation "com.mcxiaoke.volley:library:$rootProject.volleyLibrary"
    implementation "com.umeng.analytics:analytics:$rootProject.umengAnalytics"
}
